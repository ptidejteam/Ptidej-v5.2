/*-------------------------- RULES ------------------------*/

set_rules		 ::=  rule_card | set_rules rule_card
                
rule_card          ::=  RULE_CARD: string { list_rules } ;
 
list_rules         ::=  rule | list_rules rule 

rule               ::=  RULE: string { content_rule } ; | RULE: string ;

content_rule       ::=  operator string string | list_relationships | list_attributes

operator   		 ::=  INTER | UNION | DIFF | INCL | NEG


/*-------------------------- ATTRIBUTES ------------------------*/

list_attributes    ::=  attribute | operator attribute attribute
 
attribute 	       ::=  ( METRIC: id_metric, value_ordi )
					 |  ( SEMANTIC: id_semantic, value_semantic )
					 |  ( STRUCT: id_struct, value_struct ) 

id_metric 		   ::=  id_metric + id_metric | id_metric - id_metric | string

value_ordi	  	   ::=  VERY_HIGH | HIGH | MEDIUM | LOW | VERY_LOW | NONE

id_semantic		   ::=  CLASSNAME | METHODNAME | FIELDNAME

value_semantic  	   ::=  { cont_value_semantic } 

cont_value_semantic    ::=  string | string, cont_value_semantic 

id_struct		   ::=  CLASS | INTERFACE | METHOD | FIELD | PARAMETER | COMMENTS

value_struct 	   ::=  string


/*-------------------------- RELATIONSHIPS ------------------------*/
					
list_relationships ::=  relationship | relationship list_relationships 

relationship       ::=  name_relation: string FROM: string cardinality TO: string cardinality 

name_relation      ::=  ASSOC | AGGREG | COMPOS 

cardinality   	   ::=  ONE | MANY | ONE_OR_MANY | OPTIONALLY_ONE



